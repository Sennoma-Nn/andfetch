#!/data/data/com.termux/files/usr/bin/env nu

let andfetch_version = "0.0.15"

def get_ascii_colors [ icon ] {
    let icon_key = $icon  | str downcase

    let colors = {
        oneplus: ["\e[31m", "", "", ""]
        xiaomi: ["\e[38;2;255;103;0m", "", "", ""]
        redmi: ["\e[38;2;255;103;0m", "", "", ""]
        oppo: ["\e[32m", "", "", ""]
        google: ["\e[34m", "\e[33m", "\e[32m", "\e[31m"]
        vivo: ["\e[34m", "\e[37m", "", ""]
        huawei: ["\e[31m", "", "", ""]
        realme: ["\e[33m", "", "", ""]
        samsung: ["\e[34m", "\e[37m", "", ""]
    }

    if ($icon_key in $colors) {
        echo $colors | get $icon_key
    } else {
        echo ["\e[32m", "\e[37m", "", ""]
    }
}

def get_ascii_icon [ icon ] {
    let icon_key = $icon  | str downcase
    let icon_colors = get_ascii_colors $icon_key
    let ascii_icons = {

oneplus: 
$"($icon_colors.0)                      ██    
($icon_colors.0)                      ██    
($icon_colors.0)████████████████  ██████████
($icon_colors.0)██                    ██    
($icon_colors.0)██                    ██    
($icon_colors.0)██      █████               
($icon_colors.0)██         ██         ██    
($icon_colors.0)██         ██         ██    
($icon_colors.0)██         ██         ██    
($icon_colors.0)██         ██         ██    
($icon_colors.0)██      ████████      ██    
($icon_colors.0)██                    ██    
($icon_colors.0)██                    ██    
($icon_colors.0)████████████████████████    
                            "

xiaomi: 
$"($icon_colors.0) .llllllllllllllllllllllllllllllll. 
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)lllllll              'lll    lllllll
($icon_colors.0)lllllll    lllllll.    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0) 'llllllllllllllllllllllllllllllll' 
                                    "

redmi: 
$"($icon_colors.0) .llllllllllllllllllllllllllllllll. 
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)lllllll              'lll    lllllll
($icon_colors.0)lllllll    lllllll.    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)lllllll    ll    ll    ll    lllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0)llllllllllllllllllllllllllllllllllll
($icon_colors.0) 'llllllllllllllllllllllllllllllll' 
                                    "

oppo: 
$"($icon_colors.0)      .~:!7?JJ??79~.      
($icon_colors.0)   .QYPGBBGGPPGGGBBG5P.   
($icon_colors.0) .?GBBP?~'      '~7YGBGY. 
($icon_colors.0)~5BBG~              ~5BBG~
($icon_colors.0)6BGB~                ~GGB5
($icon_colors.0)6BGB~                ~PGB5
($icon_colors.0)~PBBP~              ~YBBG~
($icon_colors.0) 'JGBB57~.      .~!JGBB5' 
($icon_colors.0)   '7YGBBGGPPPPPGBBG5?'   
($icon_colors.0)      '~!7JJYYJJ?!~'      
                          "

google: 
$"($icon_colors.3)             ......            
($icon_colors.3)        .:-==========-:.       
($icon_colors.1)     .($icon_colors.3)-==================:     
($icon_colors.1)   .-===($icon_colors.3)================:'     
($icon_colors.1)  :=======($icon_colors.3)-:'      .:-:        
($icon_colors.1) -#+====-'                     
($icon_colors.1).####*+-                       
($icon_colors.1)-######         ($icon_colors.0)==============-
($icon_colors.1)=#####+         ($icon_colors.0)=+++++++++++++=
($icon_colors.1)-######         ($icon_colors.0)=============+-
($icon_colors.1)'####*+-               ($icon_colors.0).=+===+:
($icon_colors.1) -#+===+=.             ($icon_colors.0)-+===+= 
($icon_colors.1)  :=+===++($icon_colors.2)-:.      .:-($icon_colors.0)=+===+=  
($icon_colors.1)   '-++=($icon_colors.2)===++======++===($icon_colors.0)=++-   
($icon_colors.1)     '($icon_colors.2)-=+++==========+++=-($icon_colors.0)'    
($icon_colors.2)        ':-==========-:'       
($icon_colors.2)             ''''''            
                               "

vivo: 
$"($icon_colors.0) .--========================--. 
($icon_colors.0):==============================:
($icon_colors.0)-==============================-
($icon_colors.0)-==============================-
($icon_colors.0)-==============================-
($icon_colors.0)-==-($icon_colors.1)#@@@#($icon_colors.0)--==========--($icon_colors.1)#@@@#($icon_colors.0)-==-
($icon_colors.0)-===-($icon_colors.1)#@@@%-($icon_colors.0)-========-($icon_colors.1)-%@@@#($icon_colors.0)-===-
($icon_colors.0)-====-($icon_colors.1)*@@@@+($icon_colors.0)-======-($icon_colors.1)+@@@@*($icon_colors.0)-====-
($icon_colors.0)-=====-($icon_colors.1)+@@@@*($icon_colors.0)-====-($icon_colors.1)*@@@@+($icon_colors.0)-=====-
($icon_colors.0)-======-($icon_colors.1)-%@@@#($icon_colors.0)----($icon_colors.1)#@@@%-($icon_colors.0)-======-
($icon_colors.0)-=======--($icon_colors.1)#@@@%==%@@@#($icon_colors.0)--=======-
($icon_colors.0)-=========-($icon_colors.1)*@@@@@@@@*($icon_colors.0)-=========-
($icon_colors.0)-==========-($icon_colors.1)-#%@@%#-($icon_colors.0)-==========-
($icon_colors.0)-==============================-
($icon_colors.0):==============================:
($icon_colors.0) '--========================--' 
                                "

huawei: 
$"($icon_colors.0)          :+*#.  .#*+:          
($icon_colors.0)         *@@@@#  #@@@@*         
($icon_colors.0)    =+  :@@@@@@..@@@@@@:  +=    
($icon_colors.0)   *@@#: %@@@@@--@@@@@% :%@@*   
($icon_colors.0)  -@@@@@-:@@@@@==@@@@@:-@@@@@-  
($icon_colors.0)  .@@@@@@+-@@@@==@@@@-+@@@@@@.  
($icon_colors.0)=+::+%@@@@*-@@@==@@@-*@@@@%+::+=
($icon_colors.0)#@@%*=+*%@@%-#@--@#-%@@%*+=*%@@#
($icon_colors.0)'%@@@@@#*+*%%:*::*:%%*+*#@@@@@%'
($icon_colors.0)  =*#%%@@%*==:    :==*%@@%%#*=  
($icon_colors.0)   :+****##*-      -*##****+:   
($icon_colors.0)    =%@@%*-          -*%@@%=    
                                "

realme: 
$"($icon_colors.0)+*************+=.     
($icon_colors.0)*#############%@@@#-  
($icon_colors.0)                .+@@* 
($icon_colors.0)                  .@@+
($icon_colors.0)#@@@@@@@@@@@@*     *@%
($icon_colors.0)%@###########-     %@*
($icon_colors.0)#@*              -%@# 
($icon_colors.0)#@*    -*******#@@%=  
($icon_colors.0)#@*    *@@#*###*='    
($icon_colors.0)#@*    .*@#:          
($icon_colors.0)#@*      :%@#:        
($icon_colors.0)#@*        -@@*.      
($icon_colors.0)#@*          =@@+     
($icon_colors.0)#@*            +@@=   
($icon_colors.0)%@#             .*@%- 
($icon_colors.0)=+-               :+*:
                      "

samsung:
$"($icon_colors.0)       .SSSSSSSSSSSSSSSS.       
($icon_colors.0)    .SSSSSSSSSSSSSSSSSSSSSS.    
($icon_colors.0)  .SSSSSSSSSSSSSSSSSSSSSSSSSS.  
($icon_colors.0) .SSSSSSSSSS($icon_colors.1)@@@@@@@($icon_colors.0)SSSSSSSSSSS. 
($icon_colors.0).SSSSSSSSS($icon_colors.1)@@@@@@@@@@($icon_colors.0)SSSSSSSSSSS.
($icon_colors.0)SSSSSSSSS($icon_colors.1)@@@@@($icon_colors.0)SS($icon_colors.1)@@@@@($icon_colors.0)SSSSSSSSSSS
($icon_colors.0)SSSSSSSSS($icon_colors.1)@@@@@@($icon_colors.0)SSSSSSSSSSSSSSSSS
($icon_colors.0)SSSSSSSSSS($icon_colors.1)@@@@@@@@($icon_colors.0)SSSSSSSSSSSSSS
($icon_colors.0)SSSSSSSSSSSSS($icon_colors.1)@@@@@@@@($icon_colors.0)SSSSSSSSSSS
($icon_colors.0)SSSSSSSSSSSSSSS($icon_colors.1)@@@@@@@($icon_colors.0)SSSSSSSSSS
($icon_colors.0)SSSSSSSSS($icon_colors.1)@@@@@($icon_colors.0)SSS($icon_colors.1)@@@@@($icon_colors.0)SSSSSSSSSS
($icon_colors.0)'SSSSSSSSS($icon_colors.1)@@@@@@@@@@@($icon_colors.0)SSSSSSSSSS'
($icon_colors.0) 'SSSSSSSSSS($icon_colors.1)@@@@@@@($icon_colors.0)SSSSSSSSSSS' 
($icon_colors.0)  'SSSSSSSSSSSSSSSSSSSSSSSSSS'  
($icon_colors.0)    'SSSSSSSSSSSSSSSSSSSSSS'    
($icon_colors.0)       'SSSSSSSSSSSSSSSS'        
                                "

android:
$"($icon_colors.0)         -o          o-         
($icon_colors.0)          +hydNNNNdyh+          
($icon_colors.0)        +mMMMMMMMMMMMMm+        
($icon_colors.0)      `dMM($icon_colors.1)m:($icon_colors.0)NMMMMMMN($icon_colors.1):m($icon_colors.0)MMd`      
($icon_colors.0)      hMMMMMMMMMMMMMMMMMMh      
($icon_colors.0)  ..  yyyyyyyyyyyyyyyyyyyy  ..  
($icon_colors.0).mMMm`MMMMMMMMMMMMMMMMMMMM`mMMm.
($icon_colors.0):MMMM-MMMMMMMMMMMMMMMMMMMM-MMMM:
($icon_colors.0):MMMM-MMMMMMMMMMMMMMMMMMMM-MMMM:
($icon_colors.0):MMMM-MMMMMMMMMMMMMMMMMMMM-MMMM:
($icon_colors.0):MMMM-MMMMMMMMMMMMMMMMMMMM-MMMM:
($icon_colors.0)-MMMM-MMMMMMMMMMMMMMMMMMMM-MMMM-
($icon_colors.0) +yy+ MMMMMMMMMMMMMMMMMMMM +yy+ 
($icon_colors.0)      mMMMMMMMMMMMMMMMMMMm      
($icon_colors.0)      `/++MMMMh++hMMMM++/`      
($icon_colors.0)          MMMMo  oMMMM          
($icon_colors.0)          MMMMo  oMMMM          
($icon_colors.0)          oNMm-  -mMNs          
                                "

    }
    if ($icon_key in $ascii_icons) {
        echo $ascii_icons | get $icon_key
    } else {
        echo $ascii_icons | get "android"
    }
}

def mfr_info [] {
    let mfr = (getprop ro.product.manufacturer)
    echo $mfr
}

def rooted_info [] {
    if ((whoami) == "root") {
        echo "rooted"
    } else {
        let root_paths = [
            "/system/bin/su"
            "/system/xbin/su"
            "/sbin/su"
        ]

        if ($root_paths | any { |it| $it | path exists }) {
            echo "su detected"
        } else {
            echo "su not detected"
        }
    }
}

def host_name_info [] {
    let host_name = (sys host | get "name")
    echo $host_name
}

def kernel_info [] {
    let kernel_release = (uname | get "kernel-release")
    echo $kernel_release
}

def version_info [ --show_version --show_time ] {
    mut output_str = ""
    if $show_version { $output_str += $"Android (getprop ro.build.version.release)" }
    if $show_version and $show_time { $output_str += " " }
    if $show_time { $output_str += $"\((getprop ro.build.version.security_patch)\)" }
    echo $output_str
}

def machine_info [] {
    let machine = (uname | get "machine")
    echo $machine
}

def cpu_info [] {
    let output = (LANG=C lscpu)

    let cpu_list = $output | lines
    | filter { |line| $line =~ "Model name:" }
    | parse -r '^\s*Model name:\s+(.*)'
    | get capture0

    echo $cpu_list | str join ", "
}

def memory_info [] {
    open /proc/meminfo | grep MemTotal | awk '{printf "%.2f GB", $2/1024/1024}'
}

def get_shell [] {
    mut shell = ($env | get "shell"? | default "/system/bin/sh") # $SHELL 可能不存在，默认 sh
    let shell_path = $shell | split row "/"
    let path_len = $shell_path | length

    echo $shell_path | last
}

def color_info [type printable_space] {
    if $printable_space <= 0 {
        return ""
    }

    mut text = ""
    mut t = 4
    if $type == "bright" {
        $t = 10
    }

    for $j in 0..23 {
        if $j == $printable_space { break }
        $text += $"\e[($t)(($j - 2) / 3 | math ceil)m \e[0m"
    }

    echo $text
}

def print_underline [times] {
    if $times <= 0 {
        return ""
    }

    for _ in 1..$times {
        print -n "-"
    }
}

def get_first_n_chars [in_str n] {
    if $n < 0 {
        return ""
    }

    echo $in_str | str substring ..$n
}

def show_info [brand display_icon show_icon_info json] {
    let terminal_columns = term size | get "columns"
    let colors = get_ascii_colors $display_icon
    let icon = $"(get_ascii_icon $display_icon)"
    let info_list = {
        Brand: $brand
        Mfr: (mfr_info)
        Host: (host_name_info)
        Version: (version_info --show_version --show_time)
        Kernel: (kernel_info)
        Rooted: (rooted_info)
        Shell: (get_shell)
        Machine: (machine_info)
        "CPU Model": (cpu_info)
        Memory: (memory_info)
    }

    if $json == false {
        mut icon_len = ($icon | split row "\n" | length)
        mut icon_wid = ($icon | split row "\n" | last | str length)
        mut space = 4
        
        if $show_icon_info.icon {
        } else {
            $icon_len = 0
            $icon_wid = 0
            $space = 0
        }
        
        if $show_icon_info.icon {
            print -n $icon
            print -n "\n"
        }

        if $show_icon_info.info {
            if $icon_len > 0 { print -n $"\e[($icon_len)A" }
            print -n $"\e[s"

            # Title
            let print_start = ($icon_wid + $space)
            let printable_space_right = ($terminal_columns - $print_start)
            let title_version_info = (version_info --show_version)
            let title_machine_info = (machine_info)
            let title = (get_first_n_chars $"($title_version_info)   ($title_machine_info)" ($printable_space_right - 1))

            print -n $"\e[u"
            if $icon_wid + $space > 0 { print -n $"\e[($print_start)C" }
            print -n $"($colors.0)"
            print -n $title
            print -n $"\e[0m"

            # Slash in version and machine
            print -n $"\e[u"
            let slash_start = ($print_start + ($title_version_info | str length) + 1)
            if $slash_start < $terminal_columns {
                print -n $"\e[($slash_start)C"
                print -n $"\e[0m"
                print -n $"/"
            }
            print -n "\n"
            mut output_lines = 1

            # Title underline
            print -n $"\e[u"
            print -n $"\e[($output_lines)B"
            if $icon_wid + $space > 0 { print -n $"\e[($print_start)C" }
            print -n $"\e[0m"
            print_underline ([($title | str length), $printable_space_right] | math min)
            print -n "\n"
            $output_lines += 1

            # Info
            let info_list_keys = ($info_list | columns)
            for $i in 1..($info_list_keys | length) {
                let key = ($info_list_keys | get ($i - 1))
                let printable_space_value = $terminal_columns - ($print_start + ($key | str length) + 2)
                let printable_space_key = ($printable_space_right - 1)
                let key_str = (get_first_n_chars $key $printable_space_key)
                let value_str = (get_first_n_chars $": ($info_list | get $key)" ($printable_space_value + 1))

                print -n $"\e[u"
                print -n $"\e[($output_lines)B"
                if $icon_wid + $space > 0 { print -n $"\e[($print_start)C" } # 这里的 if 是因为 ESC[?C 参数即使等于 0 也会移动 1 个文字
                print -n $"($colors.0)"
                print -n $"($key_str)"
                print -n $"\e[0m"
                print -n $"($value_str)"
                print -n $"\n" # 这里的换行是为了防止终端高度不足时 ESC[?B 绘制的文字重叠
                $output_lines += 1
            }

            # Empty line between info and color display
            print -n "\n"
            $output_lines += 1

            # Color display
            for $i in 1..2 {
                let color_type = if $i == 1 { "normal" } else { "bright" }
                print -n $"\e[u"
                print -n $"\e[($output_lines)B"
                if $icon_wid + $space > 0 { print -n $"\e[($print_start)C" }
                print -n $"(color_info $color_type $printable_space_right)"
                print -n $"\n" # 这里的换行是为了防止终端高度不足时 ESC[?B 绘制的文字重叠
                $output_lines += 1
            }

            if $icon_len > $output_lines {
                print -n $"\e[u"
                print -n $"\e[($icon_len)B" # 把光标重新移动到底部
            } else {
                print -n "\n"
            }
        }
    } else {
        let show_info = if $show_icon_info.icon and $show_icon_info.info {
            echo {
                icon: $icon
                ...$info_list
            }
        } else if $show_icon_info.info {
            echo {
                ...$info_list
            }
        } else if $show_icon_info.icon {
            echo {
                icon: $icon
            }
        } else echo { }

        print ($show_info | to json -i 4)
    }
}

# System Fetch Tool for Android.
def main [
    --icon: string (-i) # Specify the ASCII icon to display
    --hide_icon         # Hide the ASCII icon and only show information
    --hide_info         # Hide information and only display the ASCII icon
    --json (-j)         # Output in JSON format
    --version (-v)      # Print the version
]: any -> string {
    if $version {
        print $andfetch_version
        exit 0
    }

    let os_name = $nu.os-info.name
    if (uname | get "operating-system") != "Android" {
        error make -u {
            msg: "This script is only allowed to run on Android."
        }
    }
    
    let brand = (getprop ro.product.brand)
    mut display_icon = $brand

    if $icon != null {
        $display_icon = $icon
    }
    
    mut show_icon_info = {
        icon: true
        info: true
    }

    if $hide_icon {
        $show_icon_info.icon = false
    }

    if $hide_info {
        $show_icon_info.info = false
    }

    show_info $brand $display_icon $show_icon_info $json
}
